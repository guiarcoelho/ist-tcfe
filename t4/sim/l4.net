CIRCUITO L4

.options savecurrents

*Rin e RL fixos
.param Rin_value=100
.param R1_value=33.7k
.param R2_value=3.6k
.param Rc_value=3.8k
.param Re_value=200
.param Rout_value=400
.param RL_value=8
.param Ci_value=800u
.param Cb_value=800u
.param Co_value=600u

.csparam Rin_value={Rin_value}
.csparam R1_value={R1_value}
.csparam R2_value={R2_value}
.csparam Rc_value={Rc_value}
.csparam Re_value={Re_value}
.csparam Rout_value={Rout_value}
.csparam RL_value={RL_value}
.csparam Ci_value={Ci_value}
.csparam Cb_value={Cb_value}
.csparam Co_value={Co_value}



* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12
Vin in 0 0 ac 1 sin(0 10m 1k)
Rin in in2 {Rin_value}

* input coupling capacitor
Ci in2 base {Ci_value}

* bias circuit
R1 vcc base {R1_value}
R2 base 0 {R2_value}

* gain stage
Q1 coll base emit BC547A
Rc vcc coll {Rc_value}
Re emit 0 {Re_value}

* bypass capacitor
Cb emit 0 {Cb_value}


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc {Rout_value}

* output coupling capacitor
Co emit2 out {Co_value}

* load
RL out 0 {RL_value}


.control

print all

op

echo "**************"
echo "NPN_TAB"
let V_CE=v(coll)-v(emit)
echo "V(CE) = $&V_CE"
let V_BE=v(base)-v(emit)
echo "V(BE) = $&V_BE"
if (V_CE>V_BE)
echo "V(CE) greater than V(BE) = Yes"
else
echo "V(CE) greater than V(BE) = No"
endif

echo "NPN_END"
echo "**************"
echo "PNP_TAB"
let V_EC=v(emit2)
echo "V(EC) = $&V_EC"
let V_EB=v(emit2)-v(coll)
echo "V(EB) = $&V_EB"
if (V_EC>V_EB)
echo "V(EC) greater than V(EB) = Yes"
else
echo "V(EC) greater than V(EB) = No"
endif
echo "PNP_END"


* frequency analysis
ac dec 10 10 100MEG

*input impedance in ohm
echo "in_imp_TAB"
let in_imp_re = Re(v(in2)[40]/i(Vin)[40])
let in_imp_im = Im(v(in2)[40]/i(Vin)[40])
let absoluto = abs(v(in2)[40]/i(Vin)[40])
echo "Input Imp = $&in_imp_re + $&in_imp_im j"
echo "Absolute Value = $&absoluto"
echo "in_imp_END"

hardcopy vo2f.ps vdb(out)-vdb(in)
meas AC maximo MAX vdb(out) from=10 to=100MEG
let valor_teste = maximo - 3

meas AC lim_inf WHEN vdb(out) = valor_teste
meas AC lim_sup WHEN vdb(out) = valor_teste CROSS=LAST

let bandwidth = lim_sup-lim_inf
let gain = maximo -vdb(in)[40]
let CO_Freq= lim_inf
let gainv = abs(v(out)[40]/v(in)[40])
let qualidade = bandwidth*gainv/CO_Freq


echo "**************"
echo "gain_band_COfreq_TAB"
echo "Gain (dB) = $&gain = dB"
echo "Gain = $&gainv = "
echo "Lower CO Freq = $&CO_Freq = Hz"
echo "Higher CO Freq = $&lim_sup = Hz"
echo "Bandwidth = $&bandwidth = Hz"
echo "gain_band_COfreq_END"

let soma_r = (Rin_value+R1_value+R2_value+Rc_value+Re_value+Rout_value+RL_value)/1000
let soma_c = (Ci_value+Cb_value+Co_value)*1000000
let soma_t = 2

let custo = soma_r+soma_c+soma_t*0.1
let merito = qualidade/custo

echo "**************"
echo "custo_merito_TAB"
echo "price = $&custo"
echo "merit = $&merito"
echo "custo_merito_END"
echo "**************"


quit
.endc
.end

