L5 Circuit

*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 10.0
Vee vee 0 -10.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

C1 in non_inv 220n

*two 220nF capacitors in series
C2 out 0 110n

X1 non_inv inv vcc vee out_amp uA741

R1 non_inv 0 1000
R2 out_amp out 1000
R3 out_amp inv 100000
R4 inv 0 1000

.op

.control

print all


* frequency analysis
ac dec 1000 10 100MEG
hardcopy vodb.ps vdb(out)
hardcopy voph.ps 180/PI*phase(v(out))

meas AC maximo MAX vdb(out) from=10 to=100MEG
meas AC fO MAX_AT vdb(out) from=10 to=100MEG
let gain= vdb(out)[2000]

echo "gain_freq_sim_TAB"
echo "AV = $&gain = dB"
echo "fO = $&fo = Hz"
echo "gain_freq_sim_END"



*input impedance in ohm

let RE_zi= RE(-v(in)[2000]/i(Vin)[2000])
let IM_zi= IM(-v(in)[2000]/i(Vin)[2000])
echo "in_imp_TAB"
echo "Input Imp = $&RE_zi + $&IM_zi j"
echo "in_imp_END"

let custo = 13323.1 + 103.66
let av_dev = abs(100-(10^(gain/20)))
let freq_dev = abs(1000-fO)
let merit = 1/(custo*(av_dev+freq_dev+1e-6))

echo "merit_TAB"
echo "AV dev = $&av_dev = "
echo "Freq dev = $&freq_dev = Hz"
echo "Cost = $&custo = MU"
echo "Merit = $&merit = "
echo "merit_END"


quit
.endc
.end
